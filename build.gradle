buildscript {
	repositories {
		mavenCentral()
		maven { url = "https://maven.minecraftforge.net" }
		maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
		maven { url = "https://plugins.gradle.org/m2" }
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
	}
}

apply plugin: "net.minecraftforge.gradle.tweaker-server"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

project.version = "1.0.5"
group = "net.thallium"
archivesBaseName = "thalliumserver"
def tweakerClass = "${group}.launch.ThalliumTweaker"

minecraft {
	version = "1.12.2"
	mappings = "stable_39"
	runDir = "run"
	tweakClass = tweakerClass
	serverJvmArgs.addAll("-Dmixin.debug=true")

	makeObfSourceJar = false

	replaceIn "MCServer.java"
	replace "@THALLIUMVERSION@", project.version
	replaceIn "launch/ThalliumLaunch.java"
	replace "@MCVERSION@", minecraft.version
	replace "@TWEAKER@", tweakerClass
}

reobf {
	shadowJar {
		mappingType = "NOTCH"
	}
}
mixin {
	defaultObfuscationEnv notch
}

sourceSets.main.ext.refMap = "mixins.thalliumserver.refmap.json"

repositories {
	maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
	mavenCentral()
}

dependencies {
	compile "net.dv8tion:JDA:4.2.0_168"
	compile "org.spongepowered:mixin:0.8.3"
	compile "org.ow2.asm:asm-debug-all:5.2"
	compile "net.sf.jopt-simple:jopt-simple:5.0.3"
	compile "com.eclipsesource.minimal-json:minimal-json:0.9.5"
	compile "org.apache.logging.log4j:log4j-api:2.8.1"
	compile "org.apache.logging.log4j:log4j-core:2.8.1"
	compile "com.google.guava:guava:21.0"
	compile "org.apache.commons:commons-lang3:3.5"
	compile "commons-io:commons-io:2.5"
	compile "com.google.code.gson:gson:2.8.0"
	provided "org.ow2.asm:asm-all:5.2"
}

shadowJar {
	classifier = ""
}
tasks.build.dependsOn(shadowJar)

jar.manifest.attributes(
		"Main-Class": "${group}.launch.ThalliumLaunch",
		"Class-Path": "minecraft_server.${minecraft.version}.jar libraries/net/minecraft/launchwrapper/1.12/launchwrapper-1.12.jar"
)